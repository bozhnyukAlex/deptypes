\import Data.Bool
\import Data.Or
\import Logic
\import hw01 (fac)
\import lect02 (List, cons, nil)

-- 1. Определите полиморфный tail для списков

\func tail {A : \Type} (xs : List A) : List A \elim xs
  | nil => nil
  | cons x xs => xs

-- 2. Реализуйте любой (but choose wisely) алгоритм сортировки. Функцию сравнения можно принимать в качестве аргумента

\func insertionSort {A : \Type} (cmp : A -> A -> Bool) (xs : List A) : List A => sortHelper cmp nil xs
  \where {
    \func sortHelper {A : \Type} (cmp : A -> A -> Bool) (result : List A) (unsorted : List A) : List A \elim unsorted
      | nil => result
      | cons x unsorted => sortHelper cmp (insertElement cmp result x) unsorted

      \where {
        \func insertElement {A : \Type} (cmp : A -> A -> Bool) (insertDst : List A) (x : A) : List A \elim insertDst
          | nil => cons x nil
          | cons h tl \as l => if (cmp x h) (cons x l) (cons h (insertElement cmp tl x))
      }
  }

-- 3. Докажите ассоциативность && и ||

\func &&-assoc (x y z : Bool) : (x and y) and z = x and (y and z) \elim x, y
  | true, true => idp
  | true, false => idp
  | false, _ => idp

\func ||-assoc (x y z : Bool) : (x or y) or z = x or (y or z) \elim x, y
  | true, _ => idp
  | false, true => idp
  | false, false => idp

-- 4. Докажите, что fac 3 равно 6

\func fac3is6 : fac 3 = 6 => idp

-- 5. Докажите следующие пропозициональные тавтологии (можно решить любые 2 из 3 задач)

-- t1 : (P and Q -> R) -> P -> Q -> R

\func t1 {P Q R : \Type} (f : \Sigma P Q -> R) (p : P) (q : Q) : R => f (p, q)

-- t2 : (P -> R) -> (Q -> R) -> P or Q -> R

\func t2 {P Q R : \Type} (f : P -> R) (g : Q -> R) (h : Or P Q) : R \elim h
  | inl h => f h
  | inr h => g h

-- t3 : ((P -> Q -> R) -> P) -> (P -> R) -> R

\func t3 {P Q R : \Type} (f : (P -> Q -> R) -> P) (g : P -> R) : R =>
  g (f \lam p _ => g p)

-- 6. Сформулируйте и докажите следующие пропозициональные тавтологии (можно решить любые 2 из 3 задач)

-- t4 : (P -> Q -> R) -> P /\ Q -> R

\func t4 {P Q R : \Type} (f : P -> Q -> R) : \Sigma P Q -> R => \lam (p, q) => f p q

-- t5 : (P or Q -> P /\ Q) -> (P -> Q) /\ (Q -> P)

\func t5 {P Q : \Type} (f : Or P Q -> \Sigma P Q) : \Sigma (P -> Q) (Q -> P) =>
  (\lam p => (f (inl p)).2, \lam q => (f (inr q)).1)

-- t6 : ((((P -> Q) -> P) -> P) -> Q) -> Q

\func t6 {P Q : \Type} (f : (((P -> Q) -> P) -> P) -> Q) : Q => f (\lam _x => _x (\lam p => f (\lam _ => p)))

-- 7. Докажите следующие утверждения

-- s1 : Если для любого x : Nat верно P(x), то существует x : Nat для которого P(x) верно

\func s1 (P : Nat -> \Type) (h : \Pi (x : Nat) -> P x) : \Sigma (x : Nat) (P x) => (42, h 42)

-- s2 : Если не существует x : Nat такого, что верно P(x), то P(3) не верно

\func s2 (P : Nat -> \Type) (h : \Sigma (x : Nat) (P x) -> Empty) : P 3 -> Empty => \lam (p3 : P 3) => h (3, p3)

-- 8. Сформулируйте и докажите следующие утверждения

-- s3 : Если для всех x : Nat верно, что P(x) влечет Q(x), и существует x : Nat для которого верно P(x),
-- то существует x : Nat для которого верно Q(x)

\func s3 (P Q : Nat -> \Type) (f : \Pi (x : Nat) -> P x -> Q x) (existsxP : \Sigma (x : Nat) (P x)) : \Sigma (x : Nat) (Q x) =>
  (existsxP.1, f existsxP.1 existsxP.2)

-- s4 : Если для всех x : Nat либо не верно P(x), либо не верно Q(x), то P(3) влечет, что Q(3) не верно

\func s4 (P Q : Nat -> \Type) (NotPOrNotQ : \Pi (x : Nat) -> Or (P x -> Empty) (Q x -> Empty)) : P 3 -> Q 3 -> Empty =>
  \lam (p3 : P 3) (q3 : Q 3) => \case NotPOrNotQ 3 \with {
    | inl p => p p3
    | inr q => q q3
  }